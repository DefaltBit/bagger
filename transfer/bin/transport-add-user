#!/usr/bin/env perl


=head1 NAME

transport-add-user - add a requester to the transport database

=head1 SYNOPSIS

transport-add-user --username ... --realname ... [OPTIONS]

  Required values:
    --username          User's login name
    --realname          User's real name

  Options:
    --help
    --email             Email Address

=head1 DESCRIPTION

transport-add-user registers a user with the database.  Once registered,
and after SSH keys have been distributed appropriately, this user will
be able to initiate transport requests with transport-queue-package.

=cut

use strict;
use warnings;
use Pod::Usage;
use Getopt::Long;
use DBI;

my %OPTIONS;

GetOptions \%OPTIONS, 'username=s', 'realname=s', 'email=s', 'help';

# print help if appropriate
pod2usage({exitval => 0, verbose => 2}) if $OPTIONS{help};

pod2usage({exitval => -1, verbose => 2}) 
    unless (defined($OPTIONS{username}) && defined($OPTIONS{realname}));

my $dbh = DBI->connect("dbi:Pg:dbname=transfer", "", "");
die "Cannot connect to database: $!" unless $dbh;

##------------------------------------------------------------

sub add_user {
    my $user = shift;
    my $real = shift;
    my $email = shift;

    my $stmt = $dbh->prepare("SELECT * FROM transport.users WHERE username=?");
    my $result = $stmt->execute($user);

    if ($stmt->rows) {
        print STDERR "Warning: user '$user' already exists.\n";
        return;
    }

    $dbh->do("INSERT INTO transport.users(username, realname, email) VALUES (?, ?, ?)", 
            {},
            $user, $real, $email);

    print "Added user '$user' to the transport database.\n";    
}

##------------------------------------------------------------

add_user($OPTIONS{username}, $OPTIONS{realname}, $OPTIONS{email});    

